cmake_minimum_required(VERSION 3.8)

project("FFTE" VERSION 7.0 LANGUAGES Fortran)

find_package(MPI REQUIRED)

if(CMAKE_Fortran_COMPILER_ID STREQUAL XL)
    set(CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -qfixed=72 -qxlf77=intarg")
endif()

add_library(ffte zdfft2d.f fft235.f kernel.f dzfft2d.f factor.f zdfft3d.f fftever.f dzfft3d.f mfft235.f zfft1d.f zfft2d.f zfft3d.f)
# CUDA (cuzfft1d.f cuzfft3d.f cuztrans.f cuzfft2d.f)

add_library(fftempi mpi/pdzfft3dv.f mpi/pzdfft3d.f mpi/pdzfft2d.f mpi/pzdfft3dv.f mpi/pzdfft2d.f mpi/pfftever.f mpi/pdzfft3d.f mpi/pfactor.f mpi/pzfft1d.f mpi/pzfft2d.f mpi/pzfft3d.f mpi/pzfft3dv.f)
# CUDA (mpi/pcuzfft1d.f mpi/pcuzfft3d.f mpi/pcuzfft3dv.f mpi/pcuzfft2d.f)
target_include_directories(fftempi PUBLIC ${MPI_Fortran_INCLUDE_DIRS})
target_link_libraries(fftempi ${MPI_Fortran_LIBRARIES})

foreach(par "" p)
    if ("${par}" STREQUAL p)
        set(dir mpi/tests/)
        set(lib fftempi ffte)
    else()
        set(dir tests/)
        set(lib ffte)
    endif()
    foreach(knd test rtest speed)
        if (${knd} STREQUAL rtest)
            set(strt 2)
        else()
            set(strt 1)
        endif()
        foreach(dim RANGE ${strt} 3)
            set(target "${par}${knd}${dim}d")
            add_executable(${target} "${dir}${par}${knd}${dim}d.f")
            set_property(TARGET ${target} PROPERTY INSTALL_RPATH_USE_LINK_PATH True)
            target_link_libraries(${target} ${lib})
            install(TARGETS ${target} DESTINATION bin)
            if ("${par}" STREQUAL p AND ${dim} EQUAL 3)
                set(target "${par}${knd}${dim}dv")
                add_executable(${target} "${dir}${par}${knd}${dim}dv.f")
                set_property(TARGET ${target} PROPERTY INSTALL_RPATH_USE_LINK_PATH True)
                target_link_libraries(${target} ${lib})
                install(TARGETS ${target} DESTINATION bin)
            endif()
        endforeach()
    endforeach()
endforeach()

install(TARGETS ffte fftempi DESTINATION lib)
